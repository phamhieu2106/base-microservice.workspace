plugins {
    id 'java'
    id 'org.springframework.boot' version "$springBootVersion"
    id 'io.spring.dependency-management' version "$springDependency"
    id 'com.google.protobuf' version '0.9.4'
    id 'application'
}

group = 'com.base'
version = '0.0.1-SNAPSHOT'


sourceSets {
    main {
        java { srcDirs = [] }
        resources { srcDirs = [] }
    }
    test {
        java { srcDirs = [] }
        resources { srcDirs = [] }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.google.protobuf'

    repositories {
        mavenCentral()
    }

    dependencies {

        implementation 'io.springfox:springfox-swagger2:3.0.0'
        implementation 'io.springfox:springfox-boot-starter:3.0.0'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.postgresql:postgresql'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.apache.commons:commons-lang3'
        implementation 'org.apache.commons:commons-collections4:4.0'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor 'org.projectlombok:lombok'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'

        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
            mavenBom "org.springframework.grpc:spring-grpc-dependencies:$springGrpcVersion"
        }
    }

    protobuf {
        protoc {
            artifact = 'com.google.protobuf:protoc'
        }
        plugins {
            grpc {
                artifact = 'io.grpc:protoc-gen-grpc-java'
            }
        }
        generateProtoTasks {
            all()*.plugins {
                grpc {
                    option 'jakarta_omit'
                    option '@generated=omit'
                }
            }
        }
    }


}

tasks.named('test') {
    useJUnitPlatform()
}

// File build.gradle
tasks.register('dockerPull', Exec) {
    group = 'docker'
    description = 'Pull Docker images using docker-compose'
    commandLine 'docker-compose', 'pull'
}

tasks.register('dockerUp', Exec) {
    group = 'docker'
    description = 'Start Docker containers using docker-compose'
    commandLine 'docker-compose', 'up', '-d'
}

tasks.register('dockerDown', Exec) {
    group = 'docker'
    description = 'Stop and remove Docker containers using docker-compose'
    commandLine 'docker-compose', 'down', '-v'
}
